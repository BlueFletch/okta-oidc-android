apply plugin: 'com.jfrog.bintray'
apply plugin: "com.github.dcendents.android-maven"
apply plugin: 'maven-publish'

version = "${rootProject.versionName}"

//produces non androidx aar
task createAndroidAar(type: Exec, dependsOn: "bundleReleaseAar") {
    def jetifier = System.getenv("ANDROID_HOME") + "/jetifier-standalone/bin/jetifier-standalone"
    commandLine jetifier, '-r', '-i', "$buildDir/outputs/aar/oidc-androidx-release.aar",
            '-o', "$buildDir/outputs/aar/oidc-android-release.aar"
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

task javadoc(type: Javadoc) {
    failOnError = false
    title = null
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

javadoc.failOnError = false
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

afterEvaluate {
    javadoc.classpath += files(android.libraryVariants.collect { variant ->
        variant.javaCompileProvider.get().classpath.files
    })
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

publishing {
    publications {
        Release(MavenPublication) {
            artifact("$buildDir/outputs/aar/oidc-android-release.aar")
            groupId "com.okta.android"
            artifactId "oidc-android"
            artifact sourcesJar {
                classifier "sources"
            }
            artifact javadocJar {
                classifier "javadoc"
            }
            version "${rootProject.versionName}"
            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')
                configurations.implementation.allDependencies.each {
                    def dependencyNode = dependenciesNode.appendNode('dependency')
                    dependencyNode.appendNode('groupId', it.group)
                    dependencyNode.appendNode('artifactId', it.name)
                    dependencyNode.appendNode('version', it.version)
                }
            }
        }
        ReleaseX(MavenPublication) {
            artifact("$buildDir/outputs/aar/oidc-androidx-release.aar")
            groupId "com.okta.android"
            artifactId "${android.defaultConfig.project.archivesBaseName}"
            artifact sourcesJar {
                classifier "sources"
            }
            artifact javadocJar {
                classifier "javadoc"
            }
            version "${rootProject.versionName}"
            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')
                configurations.implementation.allDependencies.each {
                    def dependencyNode = dependenciesNode.appendNode('dependency')
                    dependencyNode.appendNode('groupId', it.group)
                    dependencyNode.appendNode('artifactId', it.name)
                    dependencyNode.appendNode('version', it.version)
                }
            }
        }
    }
}

def isDryRun() {
    def value = project.getProperties().get("dryRun")
    return value != null ? value : "true"
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")
    configurations = ['archives']
    publications = ['Release']
    override = true
    pkg {
        repo = "com.okta.android"
        name = "okta-oidc-android"
        description = "Okta OpenID Connect & OAuth 2.0"
        userOrg = "okta"
        publicDownloadNumbers = true
        licenses = ['Apache-2.0']
        websiteUrl = "https://github.com/okta/okta-oidc-android"
        vcsUrl = "https://github.com/okta/okta-oidc-android.git"
        issueTrackerUrl = "https://github.com/okta/okta-oidc-android/issues"
        dryRun = isDryRun()
        version {
            name = "${rootProject.versionName}"
            desc = "Okta OpenID Connect & OAuth 2.0"
            vcsTag = "${rootProject.versionName}"
            released = new Date()
        }
    }
}
